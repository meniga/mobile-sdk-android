apply plugin: "digital.wup.android-maven-publish"

publishing {
    publications {
        aar(MavenPublication) {
            from components.android
            groupId project.groupIdConst
            version = project.libraryVersion
            artifactId = project.artifactIdConst

            // Tell maven to prepare the generated "*.aar" file for publishing
            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
            pom.withXml {
                def children = asNode().children()
                def packaging = children.get(children.indexOf(children.find {
                    it.name().localPart == "packaging"
                }))
                packaging.setValue("aar")
            }
        }
    }
}

artifactory {
    contextUrl = artifactory_url
    publish {
        repository {
            // The Artifactory repository key to publish to
            repoKey = artifactory_repo_key
            username = artifactory_username
            password = artifactory_password
        }
        defaults {
            // Tell the Artifactory Plugin which artifacts should be published to Artifactory.
            publications("aar")
            publishArtifacts = true
            // Properties to be attached to the published artifacts.
            properties = ["qa.level": "basic", "dev.team": "core"]
            // Publish generated POM files to Artifactory (true by default)
            publishPom = true
        }
    }
}

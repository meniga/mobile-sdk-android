apply plugin: "com.android.library"
apply plugin: "maven"
apply plugin: "digital.wup.android-maven-publish"
apply plugin: "com.jfrog.artifactory"

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.5.4"
    }
}

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.0"
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 1
        versionName "1.1.07"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "proguard-rules.pro"
    }
    buildTypes {
        release {
            minifyEnabled false
        }
        debug {
            testCoverageEnabled true
            minifyEnabled false
            testProguardFile "proguard-rules.pro"
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }

    packagingOptions {
        exclude "META-INF/LICENSE.txt"
        exclude "META-INF/NOTICE.txt"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

task copyResDirectoryToClasses(type: Copy) {
    from "${projectDir}/src/test/res"
    into "${buildDir}/intermediates/classes/test/debug/res"
}

project.ext.set("packageName", "com.meniga.sdk")
project.ext.set("libraryVersion", android.defaultConfig.versionName)

repositories {
    mavenCentral()
    google()
}

dependencies {
    implementation "com.squareup.okhttp3:okhttp:3.9.0"
    implementation "com.squareup.okhttp3:okhttp-urlconnection:3.9.0"
    api "net.danlew:android.joda:2.9.9"
    implementation "com.squareup.retrofit2:retrofit:2.3.0"
    implementation "com.squareup.retrofit2:converter-gson:2.3.0"
    implementation "com.google.code.gson:gson:2.8.2"
    implementation "javax.inject:javax.inject:1"
    implementation "javax.annotation:javax.annotation-api:1.3.1"

    testImplementation "org.robolectric:robolectric:3.3.2"
    testImplementation "net.jodah:concurrentunit:0.4.2"
    testImplementation "com.squareup.okhttp3:mockwebserver:3.8.1"
    testImplementation "com.squareup.retrofit2:retrofit-mock:2.1.0"
    testImplementation "org.assertj:assertj-core:1.7.1"
    testImplementation "junit:junit:4.12"
}

// Convenience task
task deploy() {
}
deploy.dependsOn uploadArchives

project.ext.set("groupIdConst", "com.meniga")
project.ext.set("artifactIdConst", "sdk")


// Upload to the local maven repo
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: mavenLocal().url)
            pom.project {
                groupId = project.groupIdConst
                artifactId = project.artifactIdConst
                packaging "aar"
                version =  project.libraryVersion
            }
        }
    }
}


if (project.hasProperty("artifactory_username") && (file("../artifactory.gradle").exists())) {
    apply from: "../artifactory.gradle"
}

if (project.hasProperty("bintray_user") && (file("../bintray.gradle").exists())) {
    apply from: "../bintray.gradle"
}


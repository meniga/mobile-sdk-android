apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "com.jfrog.artifactory"
apply plugin: 'digital.wup.android-maven-publish'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.5.4"
    }
}

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 1
        versionName "1.1.35"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "proguard-rules.pro"
    }
    buildTypes {
        release {
            minifyEnabled false
        }
        debug {
            testCoverageEnabled true
            minifyEnabled false
            testProguardFile "proguard-rules.pro"
        }
    }
    testOptions {
        unitTests {
            returnDefaultValues = true
            all {
                systemProperty "file.encoding", "UTF-8"
                systemProperty "user.timezone", "UTC"
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    packagingOptions {
        exclude "META-INF/LICENSE.txt"
        exclude "META-INF/NOTICE.txt"
    }
}

project.ext.set("packageName", "com.meniga.sdk")
project.ext.set("libraryVersion", android.defaultConfig.versionName)

dependencies {
    compileOnly "com.google.code.findbugs:jsr305:3.0.2"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "com.squareup.okhttp3:okhttp:3.9.1"
    implementation "com.squareup.okhttp3:okhttp-urlconnection:3.9.1"
    api "net.danlew:android.joda:2.9.9"
    implementation "com.squareup.retrofit2:retrofit:2.3.0"
    implementation "com.squareup.retrofit2:converter-gson:2.3.0"
    implementation "com.google.code.gson:gson:2.8.2"
    implementation "javax.inject:javax.inject:1"
    implementation "javax.annotation:javax.annotation-api:1.2"

    testImplementation "org.robolectric:robolectric:3.3.2"
    testImplementation "net.jodah:concurrentunit:0.4.2"
    testImplementation "com.squareup.okhttp3:mockwebserver:3.9.1"
    testImplementation "com.squareup.retrofit2:retrofit-mock:2.1.0"
    testImplementation "org.assertj:assertj-core:2.6.0"
    testImplementation "org.skyscreamer:jsonassert:1.5.0"
    //noinspection GradleDependency (https://github.com/dlew/joda-time-android/issues/148)
    testImplementation "joda-time:joda-time:2.9.4"
    testImplementation "junit:junit:4.12"
}

// Convenience task
task deploy() {
}
deploy.dependsOn publishToMavenLocal

project.ext.set("artifactIdConst", "sdk")

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
}

publishing {
    publications {
        aar(MavenPublication) {
            from components.android
            groupId rootProject.name
            artifactId = project.artifactIdConst
            version = project.libraryVersion
        }
    }
}

if (project.hasProperty("artifactory_username") && (file("../artifactory.gradle").exists())) {
    apply from: "../artifactory.gradle"
}

if (project.hasProperty("bintray_user") && (file("../bintray.gradle").exists())) {
    apply from: "../bintray.gradle"
}

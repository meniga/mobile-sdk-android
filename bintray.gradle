apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'


logger.info("BINTRAY " + project.libraryVersion)

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/sdk-release.aar")
            groupId 'com.meniga'
            artifactId 'sdk'
            version android.defaultConfig.versionName

            //The publication doesn't know about our dependencies, so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}
version = android.defaultConfig.versionName
bintray {
    user = bintray_user
    key = bintray_apikey
    dryRun = false

    pkg {
        repo = "android"
        name = "com.meniga.sdk"
        desc = "Meniga SDK library"
        websiteUrl = "https://github.com/meniga/mobile-sdk-android"
        vcsUrl = "https://github.com/meniga/mobile-sdk-android.git"
        licenses = ["MIT"]
        publish = true
        userOrg = "meniga"
        publicDownloadNumbers = true
    }
    publications = ['Production']
}

ext {
    timeoutMillis = 5000
}

createDownloadTask("updateApiJson", "http://api.umw.meniga.net/docs/user-v1", "api.json")
createDownloadTask("updateOffersJson", "http://cashbackapi.umw.meniga.net/docs/user-v1", "offers.json")

afterEvaluate {
    tasks.findAll { it.name ==~ "process.*Res" }
            .forEach { it.dependsOn updateApiJson, updateOffersJson }
}

private void createDownloadTask(String taskName, String url, String destination) {
    tasks.register(taskName) {
        group "build"

        doLast {
            try {
                download {
                    overwrite true
                    acceptAnyCertificate true
                    compress true
                    connectTimeout timeoutMillis
                    readTimeout timeoutMillis
                    src url
                    dest new File(projectDir.absolutePath + "/src/integrationTest/resources/api", destination)
                }
            } catch (Exception exception) {
                logger.warn("Skipping $url download due to failure", exception)
            }
        }
    }
}

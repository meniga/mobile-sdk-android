ext {
    timeoutMillis = 5000
}

createDownloadTask("updateApiJson", "http://api.umw.meniga.net/docs/user-v1", "api.json")
createDownloadTask("updateOffersJson", "http://cashbackapi.umw.meniga.net/docs/user-v1", "offers.json")

afterEvaluate {
    tasks.getByName("compileDebugUnitTestSources")
            .dependsOn updateApiJson, updateOffersJson
}

private void createDownloadTask(String taskName, String url, String destination) {
    task "$taskName"(type: Download) {
        group "build"
        overwrite true
        acceptAnyCertificate true
        compress true
        timeout timeoutMillis
        onlyIf { isReachable(url, timeoutMillis) }
        src url
        dest new File(projectDir.absolutePath + "/src/integrationTest/resources/api", destination)
    }
}

private boolean isReachable(String urlString, int timeOutMillis) {
    try {
        URL url = new URL(urlString)
        Socket socket = new Socket()
        socket.connect(new InetSocketAddress(url.host, url.defaultPort), timeOutMillis)
        return true
    } catch (Exception exception) {
        project.logger.warn("Skipping Swagger JSON download", exception)
        return false
    }
}
